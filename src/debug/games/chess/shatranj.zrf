(define pawn-shift (
  $1 (verify empty?)
  (if (in-zone? promotion)
      (add Fers)
   else
      add
  )
))

(define pawn-step (
  $1 (verify enemy?)
  (if (in-zone? promotion)
      (add Fers)
   else
      add
  )
))

(define step (
  $1 (verify not-friend?)
  add
))

(define leap (
  $1 $2 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty? add $1)
  (verify not-friend?)
  add
))

(game
  (title "Shatranj")
  (history "https://en.wikipedia.org/wiki/Shatranj")

  (move-sound "Audio\Pickup.wav")
  (release-sound "Audio\Pickup.wav")
  (capture-sound "")
  (drop-sound "")

  (option "animate captures"     false)
  (option "smart moves"          true)

  (players White Black)
  (turn-order White Black)

  (board
     (image "images/shatranj.bmp")
     (grid
         (start-rectangle 2 2 52 52)
         (dimensions
           ("a/b/c/d/e/f/g/h" (50 0)) ; files
           ("8/7/6/5/4/3/2/1" (0 50)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name promotion) (players White)
           (positions a8 b8 c8 d8 e8 f8 g8 h8)
     )
     (zone (name promotion) (players Black)
           (positions a1 b1 c1 d1 e1 f1 g1 h1)
     )
  )

  (piece
     (name Shah)
     (image White "images/wking.bmp"
            Black "images/bking.bmp")
     (moves
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
     )
  )
  (piece
     (name Fers)
     (image White "images/wqueen.bmp"
            Black "images/bqueen.bmp")
     (moves
         (step nw) (step sw) (step ne) (step se)
     )
  )
  (piece
     (name Rokh)
     (image White "images/wrook.bmp"
            Black "images/brook.bmp")
     (moves
         (slide n) (slide s) (slide w) (slide e)
     )
  )
  (piece
     (name Alfil)
     (image White "images/wbishop.bmp"
            Black "images/bbishop.bmp")
     (moves
         (leap nw nw) (leap ne ne) (leap sw sw) (leap se se)
     )
  )
  (piece
     (name Asb)
     (image White "images/wknight.bmp"
            Black "images/bknight.bmp")
     (moves
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
     )
  )
  (piece
     (name Sarbaz)
     (image White "images/wpawn.bmp"
            Black "images/bpawn.bmp")
     (moves
         (pawn-shift n)
         (pawn-step nw) (pawn-step ne)
     )
  )

  (board-setup
    (White 
         (Sarbaz a2 b2 c2 d2 e2 f2 g2 h2)
         (Rokh a1 h1)
         (Asb b1 g1)
         (Alfil c1 f1)
         (Fers e1)
         (Shah d1)
    )
    (Black 
         (Sarbaz a7 b7 c7 d7 e7 f7 g7 h7)
         (Rokh a8 h8)
         (Asb b8 g8)
         (Alfil c8 f8)
         (Fers e8)
         (Shah d8)
    )
  )

  (loss-condition (White Black) (checkmated Shah))
  (loss-condition (White Black) stalemated)
  (loss-condition (White Black) (pieces-remaining 1))
  (loss-condition (White Black) repetition)
)

(variant
  (title "Shatranj (Mujanah)")
; (default)
  (board-setup
    (White 
         (Sarbaz a2 b3 c4 d3 e3 f4 g3 h2)
         (Rokh b1 g1)
         (Asb c3 f3)
         (Alfil c1 f1)
         (Fers e1)
         (Shah d1)
    )
    (Black 
         (Sarbaz a7 b6 c5 d6 e6 f5 g6 h7)
         (Rokh b8 g8)
         (Asb c6 f6)
         (Alfil c8 f8)
         (Fers e8)
         (Shah d8)
    )
  )
)
