(define checker-shift (
   $1 (verify empty?)
   (if (in-zone? promotion)
      (add King)
    else
      add
   )
))

(define checker-jump (
   $1 (verify enemy?) 
   capture 
   $1 (verify empty?)
   (if (in-zone? promotion)
       (add-partial King continue-type)
    else
       (add-partial jump-type)
   )
))

(define king-shift (
   $1 (while empty?
       add $1
   )
))

(define king-jump (
   $1 (while empty?  $1)
   (verify enemy?)
   $1 (while empty?
       mark
       (while empty? 
          (opposite $1)
       ) 
       capture
       back
       (add-partial continue-type) $1
   )
))

(define king-continue (
   $1 (while empty? 
       $1 (verify not-last-from?)
   )
   (verify enemy?) 
   $1 (while empty?
       mark
       (while empty? 
          (opposite $1)
       ) 
       capture
       back
       (add-partial continue-type) $1
   )
))

(game
  (title "Russian Checkers")

  (move-sound "Audio\Pickup.wav")
  (release-sound "Audio\Pickup.wav")
  (capture-sound "")
  (drop-sound "")

  (option "animate captures"  false)
  (option "smart moves"       true)
; (option "deferred captures" true)
; (option "ko"                situation)

  (players White Black)
  (turn-order White Black)
  (move-priorities jump-type normal-type)

  (board
     (image "images/8x8.bmp")
     (grid
         (start-rectangle 2 2 52 52)
         (dimensions
           ("a/b/c/d/e/f/g/h" (50 0)) ; files
           ("8/7/6/5/4/3/2/1" (0 50)) ; ranks
         )
         (directions (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1))
     )
     (symmetry Black (nw se) (se nw) (ne sw) (sw ne))
     (zone (name promotion) (players White)
           (positions b8 d8 f8 h8)
     )
     (zone (name promotion) (players Black)
           (positions a1 c1 e1 g1)
     )
     (zone (name best) (players White)
           (positions c5 f6)
     )
     (zone (name best) (players Black)
           (positions f4 c3)
     )
     (zone (name road) (players White Black)
           (positions a1 b2 c3 d4 e5 f6 g7 h8)
     )
  )

  (piece
     (name Man)
     (image White "images/wman.bmp"
            Black "images/bman.bmp")
     (moves
         (move-type jump-type)
         (checker-jump nw) (checker-jump ne) (checker-jump sw) (checker-jump se)
         (move-type normal-type)
         (checker-shift nw) (checker-shift ne)
     )
  )
  (piece
     (name King)
     (image White "images/wdamone.bmp"
            Black "images/bdamone.bmp")
     (moves
         (move-type jump-type)
         (king-jump nw) (king-jump ne) (king-jump sw) (king-jump se)
         (move-type continue-type)
         (king-continue nw) (king-continue ne) (king-continue sw) (king-continue se)
         (move-type normal-type)
         (king-shift nw) (king-shift ne) (king-shift sw) (king-shift se)
     )
  )

  (board-setup
    (White (Man a1 c1 e1 g1  b2 d2 f2 h2  a3 c3 e3 g3) )
    (Black (Man b8 d8 f8 h8  a7 c7 e7 g7  b6 d6 f6 h6) )
  )

  (loss-condition (White Black) (pieces-remaining 0))
  (loss-condition (White Black) stalemated)
  (loss-condition (White Black) repetition)
)

(variant
  (title "80 Cells Checkers")
  (history  "Copyright (c) Nikolay Spancireti (1916-1991).")

  (board
     (image "images/10x8.bmp")
     (grid
         (start-rectangle 2 2 52 52)
         (dimensions
           ("a/b/c/d/e/f/g/h/i/j" (50 0)) ; files
           ("8/7/6/5/4/3/2/1" (0 50)) ; ranks
         )
         (directions (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1))
     )
     (symmetry Black (nw se) (se nw) (ne sw) (sw ne))
     (zone (name promotion) (players White)
           (positions b8 d8 f8 h8 j8)
     )
     (zone (name promotion) (players Black)
           (positions a1 c1 e1 g1 i1)
     )
     (zone (name best) (players White)
           (positions c5 h6)
     )
     (zone (name best) (players Black)
           (positions c3 h4)
     )
  )

  (board-setup
    (White (Man a1 c1 e1 g1 i1  b2 d2 f2 h2 j2  a3 c3 e3 g3 i3) )
    (Black (Man b8 d8 f8 h8 j8  a7 c7 e7 g7 i7  b6 d6 f6 h6 j6) )
  )
)

(variant
  (title "Northern Checkers")

  (option "animate captures"   false)
  (option "smart moves"        true)
  (option "northern-extension" true)
; (option "ko"                 situation)
)

(variant
  (title "Northern Checkers (variant)")

  (option "animate captures"   false)
  (option "smart moves"        true)
  (option "northern-extension" true)
; (option "ko"                 situation)

  (board-setup
    (White (King a1 c1 e1 g1) (Man b2 d2 f2 h2  a3 c3 e3 g3) )
    (Black (King b8 d8 f8 h8) (Man a7 c7 e7 g7  b6 d6 f6 h6) )
  )
)

(variant
  (title "Northern Kings")

  (option "animate captures"   false)
  (option "smart moves"        true)
  (option "northern-extension" true)
; (option "ko"                 situation)

  (board-setup
    (White (King a1 c1 e1 g1  b2 d2 f2 h2  a3 c3 e3 g3) )
    (Black (King b8 d8 f8 h8  a7 c7 e7 g7  b6 d6 f6 h6) )
  )
)

(variant
  (title "Column Checkers")

  (option "animate captures"           false)
  (option "smart moves"                true)
; (option "columns-checkers-extension" deferred)
)

(variant
  (title "0001")
  (default)
  (board-setup
    (White (Man b4 c3 c5 d4 g1))
    (Black (Man a5 a7 e7 f4 g3 g5 g7))
  )
)
