(define step (
  $1 (verify empty?)
  add
))

(define leap (
  $1 $2 (verify empty?)
  add
))

(define slide (
  $1 (while empty? add $1)
))

(game
  (title "Shogi Puzzle")

  (option "smart moves" false)

  (players You)
  (turn-order You)

  (board
      (image "images/puzzles/board.bmp")
      (grid
          (start-rectangle 4 4 68 69)
          (dimensions ("c/b/a" (72 0))
                      ("1/2/3" (0 72))
          )
          (directions (n 0 -1) (e 1 0) (s 0 1) (w -1 0)
                      (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1)
          )
      )
  )

  (piece 
      (name Pawn)
      (image You "images/puzzles/pl.bmp")
      (moves
         (step n)
      )
  )
  (piece 
      (name Knight)
      (image You "images/puzzles/nl.bmp")
      (moves
         (leap n nw) (leap n ne)
      )
  )
  (piece 
      (name Lance)
      (image You "images/puzzles/ll.bmp")
      (moves
         (slide n)
      )
  )
  (piece 
      (name Rook)
      (image You "images/puzzles/rb.bmp")
      (moves
         (slide n) (slide e) (slide w) (slide s)
      )
  )
  (piece 
      (name Bishop)
      (image You "images/puzzles/bl.bmp")
      (moves
         (slide nw) (slide ne) (slide sw) (slide se)
      )
  )
  (piece 
      (name Silver)
      (image You "images/puzzles/sl.bmp")
      (moves
         (step n)
         (step nw) (step ne) (step sw) (step se)
      )
  )
  (piece 
      (name Gold)
      (image You "images/puzzles/gl.bmp")
      (moves
         (step n) (step e) (step w) (step s)
         (step nw) (step ne)
      )
  )
  (piece 
      (name RookOther)
      (image You "images/puzzles/rb.bmp")
      (moves
         (slide n) (slide e) (slide w) (slide s)
      )
  )
  (piece 
      (name BishopOther)
      (image You "images/puzzles/bb.bmp")
      (moves
         (slide nw) (slide ne) (slide sw) (slide se)
      )
  )
  (piece 
      (name King)
      (image You "images/puzzles/kl.bmp")
      (moves
         (step n) (step e) (step w) (step s)
         (step nw) (step ne) (step sw) (step se)
      )
  )

  (board-setup
      (You
          (Rook        a3)
          (RookOther   c3)
          (Bishop      a1)
          (BishopOther c1)
          (Pawn     a2 c2)
          (King        b2)
      )
  )

  (win-condition (You) (and
     (absolute-config Rook        (a1))
     (absolute-config RookOther   (c1))
     (absolute-config Bishop      (c3))
     (absolute-config BishopOther (a3))
  ))
)
