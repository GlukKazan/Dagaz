(define step (
  $1 (verify empty?)
  add
))

(define bump (
  $1 (verify empty?)
  mark $1 (verify enemy?)
  (while (and enemy? (on-board? $1))
     capture
     $1
  )
  back add
))

(define pull (
  mark (opposite $1) (verify enemy?)
  (while (and enemy? (on-board? (opposite $1)))
     capture
     (opposite $1)
  )
  back $1 (verify empty?) add
))

(define bump-continue (
  $1 (verify empty?)
  (verify not-last-from?)
  mark $1 (verify enemy?)
  (while (and enemy? (on-board? $1))
     capture
     $1
  )
  back add
))

(define pull-continue (
  mark (opposite $1) (verify enemy?)
  (while (and enemy? (on-board? (opposite $1)))
     capture
     (opposite $1)
  )
  back $1 (verify empty?)
  (verify not-last-from?) add
))

(game
  (title "Fanorona")

  (move-sound "Audio/Pickup.wav")
  (release-sound "Audio/Pickup.wav")
  (capture-sound "")

  (option "smart moves"        true)
  (option "pass partial"       true)
  (option "animate captures"   false)
  (option "fanorona-extension" true)

  (players White Black)
  (turn-order White Black)
  (move-priorities priority-type normal-type)

  (piece
     (name Stone)
     (image White "images/fanorona/white.bmp"
            Black "images/fanorona/black.bmp")
     (moves
         (move-type priority-type)
         (bump n) (bump nw) (bump s) (bump se)
         (bump w) (bump sw) (bump e) (bump ne)
         (pull n) (pull nw) (pull s) (pull se)
         (pull w) (pull sw) (pull e) (pull ne)
         (move-type continue-type)
         (bump-continue n) (bump-continue nw) (bump-continue s) (bump-continue se)
         (bump-continue w) (bump-continue sw) (bump-continue e) (bump-continue ne)
         (pull-continue n) (pull-continue nw) (pull-continue s) (pull-continue se)
         (pull-continue w) (pull-continue sw) (pull-continue e) (pull-continue ne)
         (move-type normal-type)
         (step n) (step nw) (step s) (step se)
         (step w) (step sw) (step e) (step ne)
     )
  )
)
